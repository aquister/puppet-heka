# Graphs the Heka memory statistics using the heka.memstat message generated by pipeline/report.go.
#
# === Parameters:
#
# $ensure::                       This is used to set the status of the config file: present or absent
#                                 Default: present
#
### Common Filter Parameters::    Check heka::filter::sandboxfilter for the description
#
### Buffering::                   Check heka::filter::sandboxfilter for the description
#
### Common Sandbox Parameters::   Check heka::filter::sandboxfilter for the description
#
### Heka Memory Statistics Parameters
#
# $rows::                         Sets the size of the sliding window i.e., 1440 rows representing 60 seconds per row
#                                 is a 24 sliding hour window with 1 minute resolution.
#                                 Default 1440
#                                 Type: uint
#
# $sec_per_row::                  Sets the size of each bucket (resolution in seconds) in the sliding window.
#                                 Default 60
#                                 Type: uint
#
# $preservation_version::         If preserve_data = true is set in the SandboxFilter configuration, then this value should
#                                 be incremented every time the rows or sec_per_row configuration is changed to prevent the
#                                 plugin from failing to start during data restoration.
#                                 Default 0
#                                 Type: uint
#
define heka::filter::hekamemstat (
  $ensure               = 'present',
  # Common Filter Parameters
  $message_matcher      = undef,
  $message_signer       = undef,
  $ticker_interval      = undef,
  $can_exit             = undef,
  $use_buffering        = undef,
  # Buffering
  $max_file_size        = undef,
  $max_buffer_size      = undef,
  $full_action          = undef,
  $cursor_update_count  = undef,
  # Common Sandbox Parameters
  $preserve_data        = undef,
  $memory_limit         = undef,
  $instruction_limit    = undef,
  $output_limit         = undef,
  $module_directory     = undef,
  # Heka Memory Statistics Parameters
  $rows                 = 1440,
  $sec_per_row          = 60,
  $preservation_version = undef,
) {
  validate_re($ensure, '^(present|absent)$')
  # Common Filter Parameters
  if $message_matcher { validate_string($message_matcher) }
  if $message_signer { validate_string($message_signer) }
  if $ticker_interval { validate_integer($ticker_interval) }
  if $can_exit { validate_bool($can_exit) }
  if $use_buffering { validate_bool($use_buffering) }
  # Buffering
  if $max_file_size { validate_integer($max_file_size) }
  if $max_buffer_size { validate_integer($max_buffer_size) }
  if $full_action { validate_re($full_action, '^(shutdown|drop|block)$') }
  if $cursor_update_count { validate_integer($cursor_update_count) }
  # Common Sandbox Parameters
  if $preserve_data { validate_bool($preserve_data) }
  if $memory_limit { validate_integer($memory_limit) }
  if $instruction_limit { validate_integer($instruction_limit) }
  if $output_limit { validate_integer($output_limit) }
  if $module_directory { validate_string($module_directory) }
  # Heka Memory Statistics Parameters
  validate_integer($rows, $sec_per_row)
  if $preservation_version { validate_integer($preservation_version) }

  $script_type = 'lua'
  $filename = 'lua_filters/heka_memstat.lua'
  $config = {
    'rows'                 => $rows,
    'sec_per_row'          => $sec_per_row,
    'preservation_version' => $preservation_version,
  }

  $full_name = "hekamemstat_${name}"
  heka::snippet { $full_name:
    ensure  => $ensure,
    content => template("${module_name}/filter/sandboxfilter.toml.erb"),
  }
}
